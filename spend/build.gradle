apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    signingConfigs {
        config {
            storeFile file('/home/alex/StudioProjects/key.jks')
            storePassword key_store_password
            keyAlias 'spend'
            keyPassword spend_signing_password
        }
    }
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.qwert2603.spend"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 48
        versionName "5.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "Long", "BIULD_TIME", System.currentTimeMillis() + "L"
        buildConfigField "String", "BIULD_HASH", '"' + UUID.randomUUID().toString() + '"'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-snapshot"
        }
        release {
            shrinkResources true
            minifyEnabled true
//            useProguard false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-toolbar-icon'
            ]
        }
    }

    flavorDimensions "server", "aim"

    productFlavors {
        serverTest {
            dimension "server"
            applicationIdSuffix ".serverTest"
            versionNameSuffix "-serverTest"
        }
        serverProd {
            dimension "server"
            applicationIdSuffix ".serverProd"
            versionNameSuffix "-serverProd"
        }
        serverMother {
            dimension "server"
            applicationIdSuffix ".serverMother"
            versionNameSuffix "-serverMother"
        }

        forTesting {
            dimension "aim"
            applicationIdSuffix ".forTesting"
            versionNameSuffix "-forTesting"
        }
        forMarket {
            dimension "aim"
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

androidExtensions {
    experimental = true
}

dependencies {

    def appcompat_version = '1.1.0-alpha02'
    def material_version = '1.0.0'
    def core_ktx_version = '1.1.0-alpha04'
    def room_version = '2.1.0-alpha04'
    def work_version = "1.0.0-beta05"

    def andrlib_version = '1.14.2'
    def andrlib_generator_version = '1.0.10'

    def rxjava_version = '2.2.6'
    def rxandroid_version = '2.1.0'
    def rxbinding_version = '3.0.0-alpha2'

    def mosby_version = '3.1.1'
    def fragmentargs_version = '3.0.2'
    def cicerone_version = '5.0.0'

    def dagger_version = '2.21'
    def retrofit_version = '2.5.0'
    def okhttp_version = '3.13.1'

    def firebase_core_version = '16.0.7'
    def crashlytics_version = '2.9.9'
    def performance_version = '16.2.3'

    def vector_integer_view_version = '1.3.0'
    def fastscroll_version = '2.0.0'

    def stetho_version = '1.5.0'
    def junit_version = '4.12'
    def android_test_runner_version = '1.1.0-alpha4'

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"

    implementation "com.github.qwert2603:andrlib:$andrlib_version"
    implementation "com.github.qwert2603:andrlib_generator:$andrlib_generator_version"
    annotationProcessor "com.github.qwert2603:andrlib_generator:$andrlib_generator_version"
    kapt "com.github.qwert2603:andrlib_generator:$andrlib_generator_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "android.arch.work:work-runtime:$work_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.hannesdorfmann.fragmentargs:annotation:$fragmentargs_version"
    kapt "com.hannesdorfmann.fragmentargs:processor:$fragmentargs_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "ru.terrakok.cicerone:cicerone:$cicerone_version"
    implementation("com.hannesdorfmann.mosby3:mvi:$mosby_version") {
        exclude group: 'com.hannesdorfmann.mosby3', module: 'utils-fragment'
    }

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    implementation "com.jakewharton.rxbinding3:rxbinding:$rxbinding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-core:$rxbinding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxbinding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-recyclerview:$rxbinding_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation "com.google.firebase:firebase-core:$firebase_core_version"
    implementation "com.crashlytics.sdk.android:crashlytics:$crashlytics_version"
    implementation "com.google.firebase:firebase-perf:$performance_version"

    implementation "com.github.qwert2603:VectorIntegerView:$vector_integer_view_version"
    implementation "com.simplecityapps:recyclerview-fastscroll:$fastscroll_version"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test:runner:$android_test_runner_version"

    forTestingImplementation "com.facebook.stetho:stetho:$stetho_version"
}

apply plugin: 'com.google.gms.google-services'