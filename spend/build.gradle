apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.qwert2603.spenddemo"
        minSdkVersion 22
        targetSdkVersion 27
        versionCode 9
        versionName "2.0-alpha7"
        buildConfigField "Long", "BIULD_TIME", System.currentTimeMillis() + "L"

        resConfigs "en"
        resConfigs "ldltr"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-snapshot"
        }
        release {
            shrinkResources true
            minifyEnabled true
//            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "server"

    def SERVER_TYPE_TYPE = "com.qwert2603.spenddemo.model.ServerType"
    def SERVER_TYPE = "SERVER_TYPE"

    productFlavors {
        noServer {
            dimension "server"
            buildConfigField SERVER_TYPE_TYPE, SERVER_TYPE, "com.qwert2603.spenddemo.model.ServerType.NO_SERVER"
            applicationIdSuffix ".noServer"
            versionNameSuffix "-noServer"
        }
        serverTest {
            dimension "server"
            buildConfigField SERVER_TYPE_TYPE, SERVER_TYPE, "com.qwert2603.spenddemo.model.ServerType.SERVER_TEST"
            applicationIdSuffix ".serverTest"
            versionNameSuffix "-serverTest"
        }
        serverProd {
            dimension "server"
            buildConfigField SERVER_TYPE_TYPE, SERVER_TYPE, "com.qwert2603.spenddemo.model.ServerType.SERVER_PROD"
            applicationIdSuffix ".serverProd"
            versionNameSuffix "-serverProd"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(path: ':syncprocessor')

    ext.support_version = '27.0.1'
    ext.rxjava_version = '2.1.6'
    ext.rxandroid_version = '2.0.1'
    ext.rxbinding_version = '2.0.0'
    ext.mosby_version = '3.0.4'
    ext.fragmentargs_version = '3.0.2'
    ext.cicerone_version = '2.1.0'
    ext.postgresql_version = '42.1.4.jre7'
    ext.arch_components_version = '1.0.0'
    ext.dagger_version = '2.13'
    ext.junit_version = '4.12'
    ext.stetho_version = '1.5.0'

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"

    implementation "android.arch.persistence.room:runtime:$arch_components_version"
    implementation "android.arch.persistence.room:rxjava2:$arch_components_version"
    kapt "android.arch.persistence.room:compiler:$arch_components_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.hannesdorfmann.fragmentargs:annotation:$fragmentargs_version"
    kapt "com.hannesdorfmann.fragmentargs:processor:$fragmentargs_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "ru.terrakok.cicerone:cicerone:$cicerone_version"
    implementation "com.hannesdorfmann.mosby3:mvi:$mosby_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    implementation "com.jakewharton.rxbinding2:rxbinding:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxbinding_version"

    implementation "org.postgresql:postgresql:$postgresql_version"

    testImplementation "junit:junit:$junit_version"

    implementation "com.facebook.stetho:stetho:$stetho_version"
}

repositories {
    mavenCentral()
    google()
    maven { url "http://repository.jetbrains.com/all" }
    maven { url 'https://www.jitpack.io' }
}