apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.qwert2603.spenddemo_2"
        minSdkVersion 22
        targetSdkVersion 27
        versionCode 15
        versionName "3.0-alpha1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "Long", "BIULD_TIME", System.currentTimeMillis() + "L"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-snapshot"
        }
        release {
            shrinkResources true
            minifyEnabled true
//            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "server"

    def SERVER_TYPE_TYPE = "@android.support.annotation.NonNull com.qwert2603.spenddemo.model.ServerType"
    def SERVER_TYPE = "SERVER_TYPE"

    productFlavors {
        noServer {
            dimension "server"
            buildConfigField SERVER_TYPE_TYPE, SERVER_TYPE, "com.qwert2603.spenddemo.model.ServerType.NO_SERVER"
            applicationIdSuffix ".noServer"
            versionNameSuffix "-noServer"
        }
        serverTest {
            dimension "server"
            buildConfigField SERVER_TYPE_TYPE, SERVER_TYPE, "com.qwert2603.spenddemo.model.ServerType.SERVER_TEST"
            applicationIdSuffix ".serverTest"
            versionNameSuffix "-serverTest"
        }
        serverProd {
            dimension "server"
            buildConfigField SERVER_TYPE_TYPE, SERVER_TYPE, "com.qwert2603.spenddemo.model.ServerType.SERVER_PROD"
            applicationIdSuffix ".serverProd"
            versionNameSuffix "-serverProd"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(path: ':syncprocessor')

    def support_version = '27.1.1'
    def andrlib_version = '1.7.4'
    def andrlib_generator_version = '1.0.10'
    def rxjava_version = '2.1.13'
    def rxandroid_version = '2.0.2'
    def rxbinding_version = '2.1.1'
    def mosby_version = '3.1.0'
    def fragmentargs_version = '3.0.2'
    def cicerone_version = '3.0.0'
    def postgresql_version = '42.1.4.jre7'
    def dagger_version = '2.15'
    def junit_version = '4.12'
    def stetho_version = '1.5.0'
    def paging_version = "1.0.0"
    def room_version = "1.1.0"
    def lifecycle_version = "1.1.1"

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"

    implementation "com.github.qwert2603:andrlib:$andrlib_version"
    implementation "com.github.qwert2603:andrlib_generator:$andrlib_generator_version"
    annotationProcessor "com.github.qwert2603:andrlib_generator:$andrlib_generator_version"
    kapt "com.github.qwert2603:andrlib_generator:$andrlib_generator_version"

    implementation "android.arch.lifecycle:extensions:$lifecycle_version"

    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"

    implementation "android.arch.paging:runtime:$paging_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.hannesdorfmann.fragmentargs:annotation:$fragmentargs_version"
    kapt "com.hannesdorfmann.fragmentargs:processor:$fragmentargs_version"

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "ru.terrakok.cicerone:cicerone:$cicerone_version"
    implementation "com.hannesdorfmann.mosby3:mvi:$mosby_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    implementation "com.jakewharton.rxbinding2:rxbinding:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rxbinding_version"

    implementation "org.postgresql:postgresql:$postgresql_version"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    implementation "com.facebook.stetho:stetho:$stetho_version"
}

repositories {
    mavenCentral()
    google()
    maven { url "http://repository.jetbrains.com/all" }
    maven { url 'https://www.jitpack.io' }
}